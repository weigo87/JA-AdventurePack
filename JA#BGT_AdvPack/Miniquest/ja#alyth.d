ALTER_TRANS ALYTH // file name
BEGIN 2 END // state number (can be more than one)
BEGIN 0 END // transition number (can be more than one)
BEGIN // list of changes, see below for flags
  "ACTION" ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",1)~
  "JOURNAL" ~@1031~
END

ADD_STATE_TRIGGER ALYTH 4 ~!Global("JA#ELFSONG_QUEST","GLOBAL",8)
!Global("JA#ELFSONG_QUEST","GLOBAL",10)~

ALTER_TRANS ALYTH // file name
BEGIN 5 END // state number (can be more than one)
BEGIN 0 END // transition number (can be more than one)
BEGIN // list of changes, see below for flags
  "ACTION" ~StartStore("tav0705",LastTalkedToBy(Myself))~
  "REPLY" ~@69~
END

ADD_STATE_TRIGGER ALYTH 5 ~!Global("JA#ELFSONG_QUEST","GLOBAL",8)
!Global("JA#ELFSONG_QUEST","GLOBAL",10)~

EXTEND_BOTTOM ALYTH 5
IF ~~ THEN REPLY @0 GOTO JA#ALYTH_1
IF ~~ THEN REPLY @1 EXIT
END

ALTER_TRANS ALYTH // file name
BEGIN 6 END // state number (can be more than one)
BEGIN 0 END // transition number (can be more than one)
BEGIN // list of changes, see below for flags
  "ACTION" ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",1)~
  "JOURNAL" ~@1031~
END

ADD_STATE_TRIGGER ALYTH 6 ~GlobalLT("JA#ELFSONG_QUEST","GLOBAL",1)~

APPEND ALYTH

IF ~~ THEN BEGIN JA#ALYTH_1
SAY @2
IF ~Global("JA#ELFSONG_QUEST","GLOBAL",0)~ THEN REPLY @3 DO ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",1)~ JOURNAL @1031 GOTO JA#ALYTH_2
IF ~Global("JA#ELFSONG_QUEST","GLOBAL",1)~ THEN REPLY @5 GOTO JA#ALYTH_3
IF ~Global("JA#ELFSONG_QUEST","GLOBAL",3)Global("JA#WILYN_WO","LOCALS",0)~ THEN REPLY @6 DO ~SetGlobal("JA#WILYN_WO","LOCALS",1)~ GOTO JA#ALYTH_11
IF ~Global("JA#ELFSONG_QUEST","GLOBAL",6)Global("JA#VALA_WO","LOCALS",0)~ THEN REPLY @7 DO ~SetGlobal("JA#VALA_WO","LOCALS",1)~ GOTO JA#ALYTH_15
IF ~~ THEN REPLY @8 GOTO JA#ALYTH_9
IF ~~ THEN REPLY @9 GOTO JA#ALYTH_8
IF ~~ THEN REPLY @10 GOTO JA#ALYTH_10
IF ~~ THEN REPLY @11 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_2
SAY @12
IF ~Global("JA#ELFSONG_WHY","LOCALS",0)~ THEN REPLY @13 DO ~SetGlobal("JA#ELFSONG_WHY","LOCALS",1)~ GOTO JA#ALYTH_3
IF ~Global("JA#ELFSONG_WHO","LOCALS",0)~ THEN REPLY @14 DO ~SetGlobal("JA#ELFSONG_WHO","LOCALS",1)~ GOTO JA#ALYTH_4
IF ~~ THEN REPLY @15 DO ~SetGlobal("JA#ELFSONG_WHY","LOCALS",0)SetGlobal("JA#ELFSONG_WHO","LOCALS",0)~ GOTO JA#ALYTH_1
IF ~~ THEN REPLY @16 DO ~SetGlobal("JA#ELFSONG_WHY","LOCALS",0)SetGlobal("JA#ELFSONG_WHO","LOCALS",0)~ EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_3
SAY @17
IF ~Global("JA#ELFSONG_WHO","LOCALS",0)~ THEN REPLY @14 DO ~SetGlobal("JA#ELFSONG_WHO","LOCALS",1)~ GOTO JA#ALYTH_4
IF ~~ THEN REPLY @18 GOTO JA#ALYTH_7
IF ~~ THEN REPLY @19 GOTO JA#ALYTH_5
IF ~~ THEN REPLY @20 DO ~SetGlobal("JA#ELFSONG_WHY","LOCALS",0)SetGlobal("JA#ELFSONG_WHO","LOCALS",0)~ EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_4
SAY @21
IF ~Global("JA#ELFSONG_WHY","LOCALS",0)~ THEN REPLY @22 GOTO JA#ALYTH_3
IF ~~ THEN REPLY @23 GOTO JA#ALYTH_5
END

IF ~~ THEN BEGIN JA#ALYTH_5
SAY @24
IF ~~ THEN GOTO JA#ALYTH_6
END

IF ~~ THEN BEGIN JA#ALYTH_6
SAY @25
IF ~~ THEN REPLY @26 DO ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",2)~ UNSOLVED_JOURNAL #999999 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_7
SAY @28
IF ~~ THEN REPLY @23 GOTO JA#ALYTH_5
END

IF ~~ THEN BEGIN JA#ALYTH_8
SAY @29
IF ~~ THEN REPLY @30 GOTO JA#ALYTH_1
IF ~~ THEN REPLY @31 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_9
SAY @32
IF ~~ THEN REPLY @33 GOTO JA#ALYTH_1
IF ~~ THEN REPLY @31 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_10
SAY @34
IF ~~ THEN REPLY @35 GOTO JA#ALYTH_1
IF ~~ THEN REPLY @31 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_11
SAY @36
IF ~~ THEN REPLY @37 GOTO JA#ALYTH_12
IF ~~ THEN REPLY @38 GOTO JA#ALYTH_13
END

IF ~~ THEN BEGIN JA#ALYTH_12
SAY @39
IF ~~ THEN REPLY @40 GOTO JA#ALYTH_13
END

IF ~~ THEN BEGIN JA#ALYTH_13
SAY @41
IF ~~ THEN REPLY @42 GOTO JA#ALYTH_14
END

IF ~~ THEN BEGIN JA#ALYTH_14
SAY @43
IF ~~ THEN EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_15
SAY @44
IF ~~ THEN REPLY @45 GOTO JA#ALYTH_16
IF ~~ THEN REPLY @46 GOTO JA#ALYTH_17
END

IF ~~ THEN BEGIN JA#ALYTH_16
SAY @47
IF ~~ THEN REPLY @48 EXIT
END

IF ~~ THEN BEGIN JA#ALYTH_17
SAY @49
IF ~~ THEN REPLY @48 EXIT
END

IF ~Global("JA#ELFSONG_QUEST","GLOBAL",8)~ THEN BEGIN JA#ALYTH_18
SAY @50
IF ~~ THEN REPLY @51 DO ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",9)~ GOTO JA#ALYTH_19
IF ~~ THEN REPLY @52 DO ~SetGlobal("JA#ELFSONG_QUEST","GLOBAL",9)~ GOTO JA#ALYTH_20
END

IF ~~ THEN BEGIN JA#ALYTH_19
SAY @53
IF ~~ THEN REPLY @54 GOTO JA#ALYTH_20
END

IF ~~ THEN BEGIN JA#ALYTH_20
SAY @55
IF ~~ THEN REPLY @56 GOTO JA#ALYTH_21
END

IF ~~ THEN BEGIN JA#ALYTH_21
SAY @57
= @58
IF ~~ THEN DO ~EraseJournalEntry(999999)
EraseJournalEntry(999998)
EraseJournalEntry(999997)
EraseJournalEntry(999996)
EraseJournalEntry(999995)
EraseJournalEntry(999994)
EraseJournalEntry(999993)
AddExperienceParty(500)
GiveGoldForce(400)SetGlobalTimer("JA#HARP_TIMER","AR7706",ONE_DAY)~ SOLVED_JOURNAL @70 EXIT
END

IF ~Global("JA#ELFSONG_QUEST","GLOBAL",10)~ THEN BEGIN JA#ALYTH_22
SAY @59
= @60
= @61
IF ~~ THEN REPLY @62 DO ~GiveItemCreate("JA#HRP02",Myself,0,0,0)SetGlobal("JA#ELFSONG_QUEST","GLOBAL",11)~ GOTO JA#ALYTH_23
IF ~~ THEN REPLY @63 DO ~GiveItemCreate("JA#HRP02",Myself,0,0,0)SetGlobal("JA#ELFSONG_QUEST","GLOBAL",11)~ GOTO JA#ALYTH_24
END

IF ~~ THEN BEGIN JA#ALYTH_23
SAY @64
IF ~~ THEN REPLY @65 DO ~GiveItem("JA#HRP02",LastTalkedToBy(Myself))~ EXIT
END


IF ~~ THEN BEGIN JA#ALYTH_24
SAY @66
IF ~~ THEN REPLY @67 DO ~GiveItem("JA#HRP02",LastTalkedToBy(Myself))~ EXIT
IF ~~ THEN REPLY @68 DO ~GiveItem("JA#HRP02",LastTalkedToBy(Myself))~ EXIT
END

END

























