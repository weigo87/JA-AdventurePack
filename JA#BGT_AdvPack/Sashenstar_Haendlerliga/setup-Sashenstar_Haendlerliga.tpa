///////////////////////////////////
//ALDETH SASHENSTAR & HAENDLERLIGA
///////////////////////////////////

/* für BTG:EE könnte hier übertragen werden (genaues Einfügen muss noch überprüft werden):
-Extradialog, um Senyiad zu beschwichtigen
-Begegnung mit Aldeth über Deagan in Baldurs Tor (Drei alte Krüge)
-Möglichkeit, Aldeth auf seinen Verrat anzusprechen.
Änderungen in der Gilde + Verrat sind in BG:EE bereits restauriert.
*/

COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#BRANDI.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DEAGAN.D~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP.D~

COPY_EXISTING ~ALDETH.dlg~ ~override~
  LPF READ_JOURNAL_STRREF
    INT_VAR 
	state = 0
//	stateResponse = 0  //is already set to "0"
    RET strref
  END
  SET aldeth1 = strref
COPY_EXISTING ~ALDETH.dlg~ ~override~
  LPF READ_JOURNAL_STRREF
    INT_VAR state = 13
    RET strref
  END
  SET aldeth2 = strref
COPY_EXISTING ~IRLENT.DLG~ ~override~
  LPF READ_JOURNAL_STRREF
    INT_VAR state = 0
    RET strref
  END
  SET aldeth5 = strref

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#IRLENT.D~


/* bg1npc is installed: Viconia's interjection kills the new reply option to settle the discpute with Seniyad peacefully. Add the reply option to Viconia's new state, as well: */


OUTER_SET vic_state = "-4"
OUTER_SET jaheira_state = "-4"

ACTION_IF ((MOD_IS_INSTALLED ~BG1NPC/BG1NPC.TP2~ "10") //bg1npc interjections and quests is installed
		    AND (FILE_EXISTS ~bg1npc/tra/english/x#viint.tra~)) BEGIN // only checking that the bg1npc folder is still in there


PRINT ~BG1 NPC Quest und Banter Component detected~

/* look for Vici's new dialogue state. Since we don't know which language version the player installed bg1npc in, we try all of them.

We are looking for @5 in bg1npc/tra/[LANGUAGE]/x#viint.tra


And also: to make the transition to Jaheira's Shadow Druid quest, i.e. to lead to Seniyad's reply to Jaheira's greeting: @95 in bg1npc/tra/[LANGUAGE]/x#jaqu.tra

*/

/* this will grab the correct state from the language bg1npc is installed in - this is great! */
OUTER_SET jaheira_state = STATE_WHICH_SAYS 95 IN ~bg1npc/tra/%s/x#jaqu.tra~ FROM ~JAHEIRAJ.dlg~
OUTER_SET vic_state = STATE_WHICH_SAYS 5 IN ~bg1npc/tra/%s/x#viint.tra~ FROM ~viconij.dlg~

END //ACTION_IF bg1npc interjections is installed


/* Baldurdash: Aldeth's special store. The additional dialogue state for Baldurdash will be deactivated to increase compatibility. The store can be started from the original dialogue state instead. 

This has to be done before compiling the ja#aldeth.d */

COPY_EXISTING ~aldeth.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~\(Global("VP_MyBank","GLOBAL",1)\)~
		~False()~
	END
BUT_ONLY

/* If we know the state number in viconij.dlg we append to it */

<<<<<<<< ...inlined/aldeth_bg1npc.d

EXTEND_BOTTOM ~viconij~ %vic_state%
  IF ~~ THEN REPLY @0 EXTERN SENIYA JA#SENIYA_00
END

/* more bg1npc compatibility: loop to Jaheira's quest, by Lumorus */

INTERJECT SENIYA JA#SENIYA_07 JA#SENIYA_06_BG1NPC
== JAHEIRAJ IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @68 
== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @111
== JAHEIRAJ IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @112
== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @113
END JAHEIRAJ %jaheira_state%
>>>>>>>>

ACTION_IF ((vic_state >= 0) AND (jaheira_state >= 0)) BEGIN


	COMPILE EVALUATE_BUFFER 
		~...inlined/aldeth_bg1npc.d~ 
		~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ALDETH.D~
		USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~
	
	
END ELSE BEGIN //Viconia interjection from bg1npc is not installed or couldn't be detected
  
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ALDETH.D~
END


/* replace quest reward ("POTN09") with mod quest item JA#ALDET */

COPY_EXISTING ~ALDETH.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~\("POTN09"\)~
		~"JA#ALDET"~
	END
BUT_ONLY

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUALD.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUDEA.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUDEB.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUDEC.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUDED.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DEAGA.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DEAGB.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#CUIRL.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DPZRL.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP1.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ALDET.BAF~
COMPILE ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#MGUAR.BAF~

EXTEND_TOP ~AR7219.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/AR7219.BAF~
EXTEND_TOP ~AR7227.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/AR7227.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~AR7228.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/AR7228.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~AR7229.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/AR7229.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~FLAMAL.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#FFHUN.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~FFHUNT.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#FFHUN.BAF~ EVALUATE_BUFFER


COPY_EXISTING ~FLAMAL.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("ALDETH",EscapeArea())~ ~NoAction()~
	END
BUT_ONLY


EXTEND_BOTTOM ~IRLENTRE.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/IRLENTRE.BAF~ EVALUATE_BUFFER
EXTEND_BOTTOM ~ZORL.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/ZORL.BAF~ EVALUATE_BUFFER
	REPLACE ~999999~ @41

/* changed the following REPLACE_TEXTUALLY so the former extra script blocks in the area script are not needed */
COPY_EXISTING ~AR7228.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~("ALDETH",[217.491],0)~ ~("ALDETH",[1244.720],2)~
//    	REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Aldeth",SetNumTimesTalkedTo(1))~ ~Continue()~
		REPLACE_TEXTUALLY EXACT_MATCH ~NumDeadGT("DopZorl",5)~ ~NumDeadGT("DopZorl",9)
		Dead("Zorl")
		Dead("Irlentre")~
	END
BUT_ONLY

/* not needed - the original script block will be deactivated for Global("Capture800","GLOBAL",1) after Deagan spawned 
COPY_EXISTING ~AR7800.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH ~Global("Capture800","GLOBAL",0)~ ~False()~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
*/

COPY_EXISTING ~%SWBaldursGate%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~SetGlobal("SpawnDabron","GLOBAL",1)~ ~NoAction()~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("ALDETH",[3507.2477],0)~ ~Continue()~
	END
BUT_ONLY


/* Irlentree didn't change when attacked in game. Maybe the script was too slow? 
COPY_EXISTING ~IRLENTRE.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[768.607],0)~ ~NoAction()~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[710.673],0)~ ~NoAction()~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[881.661],0)~ ~NoAction()~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[942.567],0)~ ~NoAction()~
    REPLACE_TEXTUALLY EXACT_MATCH ~ForceSpell(Myself,DOPPLEGANGER_CHANGE2)~ ~UseItem("JA#DPIRL",Myself)~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES

New try:
*/

COPY_EXISTING ~IRLENTRE.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[768.607],0)~ ~UseItem("JA#DPIRL",Myself)~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[710.673],0)~ ~~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[881.661],0)~ ~~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("DOPZORL",[942.567],0)~ ~~
		REPLACE_TEXTUALLY EXACT_MATCH ~ForceSpell(Myself,DOPPLEGANGER_CHANGE2)~ ~~
	END
BUT_ONLY


COPY_EXISTING ~ZORL.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ForceSpell(Myself,DOPPLEGANGER_CHANGE2)~ ~UseItem("JA#DPZRL",Myself)~
	END
BUT_ONLY

EXTEND_TOP ~%EBaldursGate%.BCS~ ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/AR7800.BAF~

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#MLBAR.STO~ ~override~

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DEAGA.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DEAGB.CRE~ ~override~
	SAY NAME1 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)
	SAY NAME2 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP0.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP1.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP2.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP3.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP4.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP5.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP6.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP7.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP8.CRE~ ~override~
	~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DOPP9.CRE~ ~override~
	SAY NAME1 @2120
	SAY NAME2 @2120
	WRITE_ASCII 0x248 ~NONE~	#8
	WRITE_ASCII 0x250 ~NONE~	#8
	WRITE_ASCII 0x260 ~NONE~	#8
	WRITE_ASCII 0x268 ~WTASIGHT~	#8
	WRITE_ASCII 0x258 ~JA#DOPP1~	#8
	WRITE_ASCII 0x280 ~DopZorl~	#32

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DPZRL.CRE~ ~override~
	SAY NAME1 @2099
	SAY NAME2 @2099
	WRITE_ASCII 0x280 ~Zorl~	#32

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DPIRL.CRE~ ~override~
	SAY NAME1 @2100
	SAY NAME2 @2100
	WRITE_ASCII 0x280 ~Irlentre~	#32

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DPZRL.ITM~ ~override~
COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DPIRL.ITM~ ~override~

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ALDET.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ALDET.ITM~ ~override~
	SAY NAME1 @2101
	SAY NAME2 @2101
	SAY DESC @2102

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ZRLKY.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#ZRLKY.ITM~ ~override~
	SAY NAME1 @2103
	SAY NAME2 @2103
	SAY DESC @2104

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DZORL.ITM~ ~override~
	SAY NAME1 @2105
	SAY NAME2 @2105
	SAY DESC @2106

COPY ~JA#BGT_AdvPack/Sashenstar_Haendlerliga/JA#DIRLE.ITM~ ~override~
	SAY NAME1 @2107
	SAY NAME2 @2107
	SAY DESC @2108

COPY_EXISTING ~IRLENT.CRE~ ~override~
	WRITE_BYTE 0x271 255	//General
	WRITE_BYTE 0x272 103	//Race
	WRITE_BYTE 0x273 112	//Class
	WRITE_BYTE 0x275 4	//Gender
	ADD_CRE_ITEM ~JA#DPIRL~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
	REPLACE_CRE_ITEM ~CHAN04~ #0 #0 #0 ~NONE~ ~ARMOR~ EQUIP

COPY_EXISTING ~ZORL.CRE~ ~override~
	SAY NAME1 @2109
	SAY NAME2 @2109
	WRITE_BYTE 0x271 255	//General
	ADD_CRE_ITEM ~JA#ZRLKY~ #0 #0 #0 ~IDENTIFIED~ ~INV1~
	ADD_CRE_ITEM ~JA#DPZRL~ #0 #0 #0 ~IDENTIFIED~ ~LRING~

COPY_EXISTING ~MERCHG.CRE~ ~override~
	WRITE_ASCII 0x250 ~NONE~	#8 //no runaway
	WRITE_ASCII 0x258 ~JA#MGUAR~	#8

COPY_EXISTING ~BRANDI.CRE~ ~override~
	WRITE_ASCII 0x258 ~JA#MGUAR~	#8

COPY_EXISTING ~ALDETH.CRE~ ~override~
	WRITE_ASCII 0x258 ~JA#ALDET~	#8 //no runaway

COPY_EXISTING ~DABRON.CRE~ ~override/JA#DABRO.CRE~
	WRITE_ASCII 0x250 ~NONE~	#8 //no runaway
	WRITE_ASCII 0x2cc ~NONE~
	WRITE_BYTE 0x270 0xff


COPY_EXISTING ~AM0704.WAV~ ~override~

COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L1%.ARE~ ~override~
	LPF ALTER_AREA_ACTOR
		INT_VAR
		x_coord		= 914
		y_coord		= 528
		dest_x		= 914
		dest_y		= 528
		orient		= 0
		STR_VAR
		actor_name 	= ~Irlentree~
	END
	READ_SHORT 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
	//add new actors
	PATCH_DEFINE_ARRAY x_coord BEGIN 1103 1176 779 1048 688 632 711 219 402 1125 END
	PATCH_DEFINE_ARRAY y_coord BEGIN 587  577  888 757  598 576 301 487 606 500 END
	PATCH_DEFINE_ARRAY orient  BEGIN 11   2    6   8    5   13  0   14  6   3 END  
	PATCH_PHP_EACH x_coord AS index => x BEGIN
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = $x_coord("%index%")
			fj_loc_y          = $y_coord("%index%") 
			fj_dest_x         = $x_coord("%index%")
			fj_dest_y         = $y_coord("%index%") 
			fj_orientation    = $orient("%index%")     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Gast
			fj_cre_resref     = EVAL ~JA#DOPP%index%~
			fj_dlg_resref 	  = EVAL ~JA#DOPP%index%~
		END
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50 
		fj_radius          = 500
		fj_volume_variance = 40 
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
BUT_ONLY

COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2%.ARE~ ~override~
	GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
	PHP_EACH actors_array AS actors_num => actors_offset BEGIN
		READ_ASCII actors_offset+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~FECOOK~) BEGIN
			WRITE_ASCII actors_offset+0x48 ~JA#MLCOK~
		END
		READ_ASCII actors_offset+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~BRANDI~) BEGIN
			WRITE_SHORT actors_offset+0x20  637
			WRITE_SHORT actors_offset+0x22  734
			WRITE_SHORT actors_offset+0x24  637
			WRITE_SHORT actors_offset+0x26  734
			WRITE_SHORT actors_offset+0x34  14
		END
		READ_ASCII actors_offset+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~ZORL~) BEGIN
			WRITE_SHORT actors_offset+0x20  1352
			WRITE_SHORT actors_offset+0x22  671
			WRITE_SHORT actors_offset+0x24  1352
			WRITE_SHORT actors_offset+0x26  671
			WRITE_SHORT actors_offset+0x34  13
		END
	END
	//container scripts, scroll and key
	LPF ALTER_AREA_CONTAINER
		STR_VAR
		container_name = ~Container 2~
		lock_difficulty  = 100
		container_key = ~JA#ZRLKY~
	END
	READ_SHORT 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 807
		fj_loc_y          = 736
		fj_dest_x         = 807
		fj_dest_y         = 736
		fj_orientation    = 14    
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Deagan~  
		fj_cre_resref     = ~JA#DEAGA~
		fj_dlg_resref	  = ~JA#DEAGA~
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50 
		fj_radius          = 500
		fj_volume_variance = 20 
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
BUT_ONLY
		
COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L3%.ARE~ ~override~
	//delete not needed actors
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~DOPPLE~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
	READ_SHORT 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50 
		fj_radius          = 500
		fj_volume_variance = 20 
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
BUT_ONLY

COPY_EXISTING ~%UlgothsBeard_House2%.ARE~ ~override~
	PATCH_IF GAME_IS ~bgt~ BEGIN
		SET index = "-1"
		GET_OFFSET_ARRAY con_array ARE_V10_CONTAINERS 
		PHP_EACH con_array AS con_num => con_offset BEGIN
			GET_OFFSET_ARRAY2 item_array con_offset ARE_V10_ITEMS
			PHP_EACH item_array AS item_num => item_offset BEGIN
				SET index=index+1
				READ_ASCII item_offset item_name ( 8 ) NULL
				PATCH_IF (~%item_name%~ STR_EQ  ~SW1H03~) BEGIN
					LPF fj_are_structure
						INT_VAR fj_delete_mode = EVAL %index%
						STR_VAR fj_structure_type = itm
					END
				END
			END
		END
	END
	PATCH_IF GAME_IS ~eet bgee~ BEGIN
		SET index = "-1"
		GET_OFFSET_ARRAY con_array ARE_V10_CONTAINERS 
		PHP_EACH con_array AS con_num => con_offset BEGIN
			GET_OFFSET_ARRAY2 item_array con_offset ARE_V10_ITEMS
			PHP_EACH item_array AS item_num => item_offset BEGIN
				SET index=index+1
				READ_ASCII item_offset item_name ( 8 ) NULL
				PATCH_IF (~%item_name%~ STR_EQ  ~SW1H02~) BEGIN
					LPF fj_are_structure
						INT_VAR fj_delete_mode = EVAL %index%
						STR_VAR fj_structure_type = itm
					END
				END
			END
		END
	END




